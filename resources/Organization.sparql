PREFIX : <https://github.com/dbcls/grasp/ns/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX meemoo: <https://data.hetarchief.be/ns/organization/>
PREFIX schema: <https://schema.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mh:  <https://data.hetarchief.be/ns/mediahaven/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?iri :iri   ?iri;
  :id         ?id;
  :label      ?label;
  :alt_label  ?alt_label ;                                               
  :mam_label  ?mam_label;
  :slug ?slug;                           
  :homepage   ?homepage;                             
  :description ?description;                            
  :logo         ?logo;                                   
  :overlay      ?overlay;
  :bzt      ?bzt;       
  :form_url      ?fixed_form_url;                          
  :account_manager ?account_manager;                         
  :classification   ?classification;
  :sector ?sector;
  :units     ?unit;                
  :contact_point ?contact_point;                    
  :primary_site  ?primary_site;                          
  :sites ?site;                                  
  :posts ?post;  
  :category ?cat.
}
FROM <https://data.hetarchief.be/graph/organizations>
FROM <https://data.hetarchief.be/graph/organizations/schema>
WHERE
{
  { 
    SELECT ?iri ?id 
    WHERE {
      {{#if type}}
        ?iri rdf:type/rdfs:subClassOf* meemoo:{{type}} .
      {{else}}
        ?iri a org:Organization .
      {{/if}}
      ?iri schema:identifier ?id .
      # Only OR ids
      FILTER strStarts(?id, "OR-")
      
      {{#if id}}
      ?iri schema:identifier "{{id}}" .
      {{/if}}

      {{#if iri}}
      VALUES ?iri { {{join " " (as-iriref iri)}} }
      {{/if}}

      # label filter
      {{#if label}}
      ?iri skos:prefLabel ?label .
      FILTER (contains(lcase(str(?label)), lcase({{as-string label}})))
      {{/if}}
    }
    {{#if offset includeZero=true}}
    ORDER BY ASC(?iri)
    OFFSET {{offset}}
    {{/if}}
    {{#if limit includeZero=true}}
    LIMIT {{limit}}
    {{/if}}
  }
  OPTIONAL { ?iri skos:prefLabel ?label }
  OPTIONAL { ?iri skos:altLabel ?alt_label }
  OPTIONAL { ?iri mh:label ?mam_label }
  OPTIONAL { ?iri foaf:homepage ?homepage }
  OPTIONAL { ?iri dcterms:description ?description }
  OPTIONAL { ?iri schema:logo ?logo }
  OPTIONAL { ?iri meemoo:allowsOverlay ?overlay }
  OPTIONAL { ?iri meemoo:allowsBZT ?bzt }
  OPTIONAL { ?iri meemoo:requestForm ?form_url } 
  OPTIONAL { ?iri meemoo:sector ?sector } 
  OPTIONAL { ?iri meemoo:hasAccountManager ?account_manager }
  OPTIONAL { ?iri org:classification ?classification }
  OPTIONAL { ?iri org:hasUnit ?unit }
  OPTIONAL { ?iri schema:contactPoint ?contact_point }
  OPTIONAL { ?iri org:hasPrimarySite ?primary_site }
  OPTIONAL { ?iri org:hasSite ?site }
  OPTIONAL { ?iri org:hasPost ?post }

  # fix form_url
  BIND (REPLACE(?form_url,"\\\\_","_") AS ?fixed_form_url)
  
  # add category
  BIND (
    IF(EXISTS { ?iri a meemoo:ContentPartner }, "Content Partner", 
      IF(EXISTS { ?iri a meemoo:ServiceProvider }, "Service Provider", 
      IF(EXISTS { ?iri a meemoo:ServiceConsumer }, "Customer", ?x)
    )) AS ?cat)

  # create slug
  BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(
  REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(lcase(?label), 
  "[àáâãäåæ]", "a", "i"),
  "[èéêë]", "e", "i"),
  "[ìíîï]", "i", "i"),
  "[òóôõöœø]", "o", "i"),
  "[ùúûü]", "u", "i"),
  "[ñ]", "n", "i"),
  "[ç]", "c", "i"),
  "[ß]", "s", "i"),
  "[ÌÍÎÏÈÉÊËÒÓÔÕÖŒÙÚÛÜÑÇ]", "", "i"),
  "[^a-z0-9]+", "-", "i"),
  "^-|-$", "", "i") AS ?slug)
}