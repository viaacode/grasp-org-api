PREFIX : <https://github.com/dbcls/grasp/ns/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX haOrg: <https://data.hetarchief.be/ns/organization/>
PREFIX haPref: <https://data.hetarchief.be/id/organization-preferences/>
PREFIX schema: <https://schema.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mh:  <https://data.hetarchief.be/ns/mediahaven/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?iri :iri   ?iri;
  :id         ?id;
  :label      ?label;
  :alt_label  ?alt_label ;                                               
  :mam_label  ?mam_label;
  :slug ?slug;                           
  :homepage   ?homepage;                             
  :description ?description;                            
  :logo         ?logo;                                   
  :overlay      ?overlay;
  :bzt      ?bzt;      
  :iiif      ?iiif;
  :form_url      ?fixed_form_url;                          
  :account_manager ?account_manager;                         
  :classification   ?classification;
  :sector ?sector;
  :units     ?unit;                
  :contact_point ?contact_point;                    
  :primary_site  ?primary_site;                          
  :sites ?site;                                  
  :posts ?post;  
  :category ?cat.
}
FROM <https://data.hetarchief.be/graph/organization>
FROM <https://data.hetarchief.be/graph/organization-preferences>
FROM <https://data.hetarchief.be/graph/thesauri>
FROM <https://data.hetarchief.be/graph/organization-static-bavo>
WHERE
{
  { 
    SELECT ?iri ?id 
    WHERE {
      {{#if type}}
        ?iri rdf:type/rdfs:subClassOf* haOrg:{{type}} .
      {{else}}
        ?iri a org:Organization .
      {{/if}}

      ?iri org:identifier ?id .
      # Only OR ids
      FILTER strStarts(?id, "OR-")

      FILTER NOT EXISTS { ?iri a org:OrganizationalUnit }
      
      {{#if id}}
      ?iri org:identifier "{{id}}" .
      {{/if}}

      {{#if sector}}
        BIND(REPLACE("{{sector}}","_"," ") AS ?sec)
        ?iri haOrg:sector ?sec .
      {{/if}}

      {{#if iri}}
      VALUES ?iri { {{join (as-iriref iri) " "}} }
      {{/if}}

      # label filter
      {{#if label}}
      ?iri skos:prefLabel ?label .
      FILTER (contains(lcase(str(?label)), lcase({{as-string label}})))
      {{/if}}
    }
    {{#if offset includeZero=true}}
    ORDER BY ASC(?iri)
    OFFSET {{offset}}
    {{/if}}
    {{#gt limit 1000}}
    LIMIT 1000
    {{else}}
    {{#if limit includeZero=true}}
    LIMIT {{limit}}
    {{else}}
    LIMIT 1000
    {{/if}}
    {{/gt}}
  }
  OPTIONAL { ?iri skos:prefLabel ?label }
  OPTIONAL { ?iri skos:altLabel ?alt_label }
  OPTIONAL { ?iri mh:tenantName ?mam_label }
  OPTIONAL { ?iri foaf:homepage ?homepage }
  OPTIONAL { ?iri dcterms:description ?description }
  OPTIONAL { ?iri haOrg:hasLogo ?logo }
  OPTIONAL { ?iri haOrg:requestForm ?form_url } 
  OPTIONAL { ?iri haOrg:sector ?sector } 
  OPTIONAL { ?iri haOrg:hasAccountManager ?account_manager }
  OPTIONAL { ?iri org:classification ?classification }
  OPTIONAL { ?iri org:hasUnit ?unit }
  OPTIONAL { ?iri schema:contactPoint ?contact_point }
  OPTIONAL { ?iri org:hasPrimarySite ?primary_site }
  OPTIONAL { ?iri org:hasSite ?site }
  OPTIONAL { ?iri org:hasPost ?post }

  BIND (EXISTS { ?iri haOrg:hasPreference haPref:logo-embedding } AS ?overlay)
  BIND (EXISTS { ?iri haOrg:hasPreference haPref:visitor-space-publication } AS ?bzt)
  BIND (EXISTS { ?iri haOrg:hasPreference haPref:iiif-dissemination } AS ?iiif)
  
  # fix form_url
  BIND (REPLACE(?form_url,"\\\\_","_") AS ?fixed_form_url)
  
  # add category
  BIND (
    IF(EXISTS { ?iri a haOrg:ContentPartner }, "Content Partner", 
      IF(EXISTS { ?iri a haOrg:ServiceProvider }, "Service Provider", 
      IF(EXISTS { ?iri a haOrg:ServiceConsumer }, "Customer", "Other")
    )) AS ?cat)

  # create slug
  BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(
  REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(lcase(?label), 
  "[àáâãäåæ]", "a", "i"),
  "[èéêë]", "e", "i"),
  "[ìíîï]", "i", "i"),
  "[òóôõöœø]", "o", "i"),
  "[ùúûü]", "u", "i"),
  "[ñ]", "n", "i"),
  "[ç]", "c", "i"),
  "[ß]", "s", "i"),
  "[ÌÍÎÏÈÉÊËÒÓÔÕÖŒÙÚÛÜÑÇ]", "", "i"),
  "[^a-z0-9]+", "-", "i"),
  "^-|-$", "", "i") AS ?slug)
}